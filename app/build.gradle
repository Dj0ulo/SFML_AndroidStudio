apply plugin: 'com.android.application'

def ndkDir = System.getenv("ANDROID_NDK")
def propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    Properties properties = new Properties()
    properties.load(propertiesFile.newDataInputStream())
    ndkDir = properties.getProperty('ndk.dir')
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "sfml.com.sfml_example"
        //Replace this with your real package name (e.g. com.StudioName.GameName)
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14 -frtti -fexceptions"
                arguments "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_STL=c++_shared",
                        "-DSFML_INCLUDE_DIR=${ndkDir}/sources/third_party/sfml/include",
                        "-DSFML_LIBRARY_DIR=${ndkDir}/sources/third_party/sfml/lib/",
                        "-DSFML_EXT_LIBRARY_DIR=${ndkDir}/sources/third_party/sfml/extlibs/lib/"
                abiFilters 'armeabi-v7a'
                // multiple ABIs are supported
                // but you have to compile SFML for every ABI you want to support
                // abiFilters 'armeabi-v7a',  'armeabi'
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            jniDebuggable true
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            // let gradle pack the shared library into apk
            jniLibs.srcDirs = ["${ndkDir}/sources/sfml/lib", "${ndkDir}/sources/sfml/extlibs/lib/"]
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}




